apply plugin: 'com.android.application'

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "ai.elimu.vitabu"
        minSdkVersion 24
        targetSdkVersion 32
        versionCode 1001004
        versionName "1.1.4-SNAPSHOT"
        setProperty("archivesBaseName", "${applicationId}-${versionCode}")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        qa_test {
            // keytool -genkeypair -v -keystore my-debug-key.keystore -alias my-key-alias -keyalg RSA -keysize 2048 -validity 10000
            // ./gradlew assembleQa_test -Dpassword=123456 -Dstorefile=/home/sxm/gitcoin/vitabu/sxm/vitabu/app/my-debug-key.keystore
            storeFile file(System.getProperty('storefile'))
            storePassword System.getProperty('password')
            keyAlias 'my-key-alias'
            keyPassword System.getProperty('password')
        }
        release {
            // keytool -genkeypair -v -keystore my-release-key.keystore -alias my-key-alias -keyalg RSA -keysize 2048 -validity 10000
            // ./gradlew assembleRelease -Dpassword=123456 -Dstorefile=/home/sxm/gitcoin/vitabu/sxm/vitabu/app/my-release-key.keystore
            storeFile file(System.getProperty('storefile'))
            storePassword System.getProperty('password')
            keyAlias 'my-key-alias'
            keyPassword System.getProperty('password')
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders = [contentProviderApplicationId: "ai.elimu.content_provider.debug"]
            buildConfigField("String", "CONTENT_PROVIDER_APPLICATION_ID", '"ai.elimu.content_provider.debug"')
            buildConfigField("String", "ANALYTICS_APPLICATION_ID", '"ai.elimu.analytics.debug"')
        }
        qa_test {
            initWith debug
            applicationIdSuffix ".test"
            versionNameSuffix "-test"
            manifestPlaceholders = [contentProviderApplicationId: "ai.elimu.content_provider.test"]
            buildConfigField("String", "CONTENT_PROVIDER_APPLICATION_ID", '"ai.elimu.content_provider.test"')
            buildConfigField("String", "ANALYTICS_APPLICATION_ID", '"ai.elimu.analytics.test"')
            signingConfig signingConfigs.qa_test
        }
        release {
            minifyEnabled false
            manifestPlaceholders = [contentProviderApplicationId: "ai.elimu.content_provider"]
            buildConfigField("String", "CONTENT_PROVIDER_APPLICATION_ID", '"ai.elimu.content_provider"')
            buildConfigField("String", "ANALYTICS_APPLICATION_ID", '"ai.elimu.analytics"')
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.github.elimu-ai:model:model-2.0.53' // See https://jitpack.io/#elimu-ai/model
    implementation 'com.github.elimu-ai:content-provider:1.2.10@aar' // See https://jitpack.io/#elimu-ai/content-provider
    implementation 'com.github.elimu-ai:analytics:3.1.11@aar' // See https://jitpack.io/#elimu-ai/analytics

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.google.android:flexbox:2.0.1'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
