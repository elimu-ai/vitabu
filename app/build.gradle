import java.util.regex.Matcher
import java.util.regex.Pattern

apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

android {
    compileSdk 35
    namespace "ai.elimu.vitabu"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 35
        versionCode 1002047
        versionName "1.2.47"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders = [contentProviderApplicationId: "ai.elimu.content_provider.debug"]
            buildConfigField("String", "CONTENT_PROVIDER_APPLICATION_ID", '"ai.elimu.content_provider.debug"')
            buildConfigField("String", "ANALYTICS_APPLICATION_ID", '"ai.elimu.analytics.debug"')
            buildConfigField("String", "APPSTORE_APPLICATION_ID", '"ai.elimu.appstore.debug"')
        }
        release {
            minifyEnabled false
            manifestPlaceholders = [contentProviderApplicationId: "ai.elimu.content_provider"]
            buildConfigField("String", "CONTENT_PROVIDER_APPLICATION_ID", '"ai.elimu.content_provider"')
            buildConfigField("String", "ANALYTICS_APPLICATION_ID", '"ai.elimu.analytics"')
            buildConfigField("String", "APPSTORE_APPLICATION_ID", '"ai.elimu.appstore"')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig = true
        viewBinding true
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation libs.elimu.content.provider // https://jitpack.io/#ai.elimu/content-provider

    implementation libs.material
    implementation libs.androidx.constraint.layout
    implementation libs.androidx.recyclerview
    implementation libs.flexbox

    implementation libs.glide

    implementation libs.kotlin.coroutines
    implementation libs.hilt.android
    implementation (libs.shine.button) {
        exclude group: 'com.daasuu', module: 'EasingInterpolator'
    }
    implementation libs.easing.interpolator
    kapt libs.hilt.compiler

    testImplementation libs.junit

    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso
}

publishing {
    publications {
        apk(MavenPublication) {
            artifact("${buildDir}/outputs/apk/release/app-release-unsigned.apk")
        }
    }
}
tasks.named("publishApkPublicationToMavenLocal") {
    mustRunAfter(":app:assembleRelease")
}

tasks.register('removeSnapshot') {
    doLast {
        println("removeSnapshot")

        def file = file("build.gradle")
        def originalFileContent = file.getText()

        Pattern pattern = Pattern.compile("versionName \"\\d+\\.\\d+\\.\\d+-SNAPSHOT\"")
        Matcher matcher = pattern.matcher(originalFileContent)
        matcher.find()
        println("match: ${matcher.group()}")

        def newVersionName = matcher.group().replace("-SNAPSHOT", "")
        println("newVersionName: ${newVersionName}")

        def newFileContent = originalFileContent.replaceFirst("versionName \"\\d+\\.\\d+\\.\\d+-SNAPSHOT\"", newVersionName)
        file.write(newFileContent)
    }
}
tasks.register('getVersionName') {
    doLast {
        println android.defaultConfig.versionName
    }
}
tasks.register('bumpVersion') {
    doLast {
        println("bumpVersion")

        def currentVersionCode = android.defaultConfig.versionCode
        println("currentVersionCode: ${currentVersionCode}")

        def newVersionCode = currentVersionCode + 1
        println("newVersionCode: ${newVersionCode}")

        def newVersionName = newVersionCode.toString().substring(0, 1).toInteger() + "." + newVersionCode.toString().substring(1, 4).toInteger() + "." + newVersionCode.toString().substring(4, 7).toInteger()
        println("newVersionName: ${newVersionName}")

        def file = file("build.gradle")
        def originalFileContent = file.getText()
        def newFileContent = originalFileContent.replaceFirst("versionCode \\d+", "versionCode ${newVersionCode}")
        newFileContent = newFileContent.replaceFirst("versionName \"\\d+\\.\\d+\\.\\d+\"", "versionName \"${newVersionName}\"")
        file.write(newFileContent)
    }
}
tasks.register('addSnapshot') {
    doLast {
        println("addSnapshot")

        def file = file("build.gradle")
        def originalFileContent = file.getText()

        Pattern pattern = Pattern.compile("versionName \"\\d+\\.\\d+\\.\\d+\"")
        Matcher matcher = pattern.matcher(originalFileContent)
        matcher.find()
        println("match: ${matcher.group()}")

        def newVersionName = "${matcher.group().substring(12, matcher.group().length() - 1)}-SNAPSHOT\""
        println("newVersionName: ${newVersionName}")

        def newFileContent = originalFileContent.replaceFirst("versionName \"\\d+\\.\\d+\\.\\d+\"", "versionName ${newVersionName}")
        file.write(newFileContent)
    }
}
